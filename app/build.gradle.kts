plugins {
    id("com.android.application")
    kotlin("android")
    kotlin("kapt")
}

android {

    /**
     * Encapsulates signing configurations that you can apply to BuildType and ProductFlavor configurations.
     */
    signingConfigs {

        getByName("debug") {
            keyAlias = "androiddebugkey"
            keyPassword = "android"
            storePassword = "android"
            storeFile = rootProject.file("settings/keystore/debug.jks")
        }

        create("release") {
            keyAlias = "playfinity2018"
            keyPassword = "playfinity2018"
            storePassword = "playfinity2018"
            storeFile = rootProject.file("settings/keystore/release.jks")
        }
    }

    /**
     * Encapsulates all build type configurations for this project.
     */
    buildTypes {

        debug {

            // Specifies whether to enable code shrinking for this build type.
            isMinifyEnabled = false

            // Specifies signing config for this variant.
            signingConfig = signingConfigs.getByName("debug")

            // Specifies version name suffix, that will be appended to the "base"
            // version name when calculating the final version name for this variant.
            versionNameSuffix = " Internal"
        }

        release {

            // Specifies whether to enable code shrinking for this build type.
            isMinifyEnabled = true

            // Specifies signing config for this variant.
            signingConfig = signingConfigs.getByName("release")
        }
    }

    /**
     * Specifies Java compiler options, such as the language level of the Java source code and generated bytecode.
     */
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    /**
     * Specifies the API level to compile your project against.
     */
    compileSdk = 34

    /**
     * Specifies defaults for variant properties that the Android plugin applies to all build variants.
     */
    defaultConfig {
        applicationId = "register.before.production.android"
        minSdk = 21
        targetSdk = 34
        versionCode = 10001
        versionName = "1.0.0"

        // Adds a new generated app name resource's.
        resValue("string", "PFI_APP_KEY", "SAMPLE_TEST")
        resValue("string", "PFI_DEV_KEY", "SAMPLE_TEST")
        resValue("string", "PFI_API_URL", "https://apps.playfinity.io/")
        resValue("string", "PFI_CLOUD_API_URL", "https://cloud.playfinity.io/")
    }

    /**
     * Specifies module namespace.
     */
    namespace = "com.playfinity.recorder"
}

dependencies {
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    // For Smartball, we need to include Playfinity SDK with dependencies:
    implementation(fileTree(mapOf("dir" to "libs", "include" to listOf("*.jar", "*.aar"))))
    implementation("androidx.room:room-runtime:2.6.1")
    implementation("com.jakewharton.timber:timber:5.0.1")
    implementation("com.squareup.okhttp3:logging-interceptor:4.10.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1")
    kapt("androidx.room:room-compiler:2.6.1")
}
